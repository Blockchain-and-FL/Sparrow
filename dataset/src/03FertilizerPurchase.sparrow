Contract FertilizerPurchase{
	PA Person
	CA Ca
	Token token{
	  string name,	
	  uint number,
	  ufixed unit_price,
	  uint unit_weight,
	  uint price,
	  string other
	}
	Init {
		Person Buyer = ('JingMen City DongBao District Agriculture and Rural Affairs Bureau',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		Person Seller = ('HuBei NanGuo Chemical Co., Ltd.',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		Ca Arbitration = ('arbitration institution',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4'),
		token Service =('fertilizer',6000,91.25,25,547500,'=30-0-15') 
	}
	ContractMessage{
	  string place='',
	  uint giveTime=0,
	  string faultName='',
	  ufixed amount=0,
	  bool checkResult=false,
	  duration period=5 days,
	  uint dayNum=0,
	  ufixed Proportion=0.005
	}
	
	Operations[
		end:assign(ContractState,finish),
		end2: assign(ContractState,terminate)
	]
	
	Rules{
		//Buyer notifies delivery time and place
		rule1:
		SetMessage(string place,uint giveTime)
		Buyer;
		
		//Seller delivers on time
		rule2:
		If: isDone(rule1) and logic(now,giveTime,==),
		Seller;
		
		//Seller delays delivery but not over 5 days
		rule2_2:
		SetMessage(uint dayNum)
		If: isTime(giveTime) and !isTime(giveTime+period) and !isDone(rule2),
		Seller transfer(Buyer,dayNum*Proportion*o Service.price)
		
		//Seller delays over 5 days, contract terminates
		rule2_3:
		If: isTime(giveTime+period) and !isDone(rule2) and !isDone(rule2_2),
		Buyer assign(ContractState,terminate);
		
		//Buyer confirms receipt, if quality fails return and terminate
		rule3:
		SetMessage(bool checkResult)
		If: isDone(rule2) and within 15 days after rule2,
		Buyer
		SubRule{
			If:!isTrue(checkResult),
			assign(ContractState,terminate)
		};
		
		//Quality OK, seller processes reimbursement
		rule4:
		If: isDone(rule3),
		Seller;
		
		//Buyer pays, contract ends
		rule5:
		If: isDone(rule4),
		Buyer transfer(Seller,o Service.price) and end
		
		//Arbitration decides liable party, liable party pays penalty
		Additional[
		arbitrationClause:
		SetMessage(string faultName,ufixed amount)
		Arbitration;
		
		buyer_fault:
		If: isDone(arbitrationClause) and logic(faultName,s Buyer.name,==),
		Buyer transfer(Seller,amount) and end2;
		
		seller_fault:
		If: isDone(arbitrationClause) and logic(faultName,s Seller.name,==),
		Seller transfer(Buyer,amount) and end2
		]
	}
	
	Require:isDone,isTime,isTrue,transfer;
}
