Contract FertilizerPurchase{
	PA Person
	CA Ca
	Token token{
	  string name,	
	  uint number,
	  ufixed unit_price,
	  uint unit_weight,
	  uint price,
	  string other
	}
	init {
		s Person Buyer = ('JingMen City DongBao District Agriculture and Rural Affairs Bureau',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		s Person Seller = ('HuBei NanGuo Chemical Co., Ltd.',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		s Ca Arbitration = ('arbitration institution',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		o token Service =('fertilizer',6000,91.25,25,547500,'=30-0-15') 
	}
	ContractMessage{
	  string place='',
	  uint giveTime=0,
	  string faultName='',
	  ufixed amount=0,
	  bool checkResult=false,
	  duration period=5 days,
	  uint dayNum=0,
	  ufixed Proportion=0.005
	}
	
	Operations[
		end:change(ContractState,finish),
		end2: change(ContractState,terminate)
	]
	
	Rule{
		// Buyer specifies delivery time and place
		rule1:
		setMessage(string place,uint giveTime)
		Buyer;
		
		// Seller delivers goods as per the specified time
		rule2:
		Con: isDone(rule1) and logic(now,giveTime,==),
		Seller;
		
		// Seller delays delivery but within five days
		rule2_2:
		setMessage(uint dayNum)
		Con: isTime(giveTime) and !isTime(giveTime+period) and !isDone(rule2),
		Seller transfer(Buyer,dayNum*Proportion*o Service.price);
		
		// Seller delays delivery for more than five days, contract terminates
		rule2_3:
		Con: isTime(giveTime+period) and !isDone(rule2) and !isDone(rule2_2),
		Buyer change(ContractState,terminate);
		
		// Buyer confirms receipt, returns goods if quality is unqualified, and terminates contract
		rule3:
		setMessage(bool checkResult)
		Con: isDone(rule2) and within 15 days after rule2,
		Buyer
		sub{
			Con:!isTrue(checkResult),
			change(ContractState,terminate);
		};
		
		// Quality is satisfactory, Seller invoices Buyer
		rule4:
		Con: isDone(rule3),
		Seller;
		
		// Buyer makes payment, contract ends
		rule5:
		Con: isDone(rule4),
		Buyer transfer(Seller,o Service.price) and end;
		
		// Arbitration institution arbitrates fault, faulting party pays fine
		Addition[
		arbitrationClause:
		setMessage(string faultName,ufixed amount)
		Arbitration;
		
		buyer_fault:
		Con: isDone(arbitrationClause) and logic(faultName,s Buyer.name,==),
		Buyer transfer(Seller,amount) and end2;
		
		seller_fault:
		Con: isDone(arbitrationClause) and logic(faultName,s Seller.name,==),
		Seller transfer(Buyer,amount) and end2;
		]
		
	}
	
	require:isDone,isTime,isTrue,transfer;
}
