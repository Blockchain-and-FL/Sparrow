Contract LightRent{
	PA Person
	Token token{
	  string name,	
	  uint price
	}
	init {
		s Person Buyer = ('LuoTian County HuangMei Drama Group',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		s Person Seller = ('GuangYing Electronic Products Sales Department, BaoHe District, HeTei City',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		o token Service =('Performance services',64807) 
	}
	ContractMessage{
	  ufixed extend1=10000,
	  ufixed extend2=20000,
	  ufixed default=7710,
	  ufixed Proportion=0.4,
	  uint dayNum=0,
	  date signTime=2021/12/15,00:00,
	  duration period=2 days,
	  string signPlace='HuangGang City, HuBei Province, HeFei',
	  date startTime=2021/12/20,00:00,
	  date finishTime=2021/12/22,00:00,
	  bool checkStatus=false,
	  ufixed deviceAmount=0,
	  ufixed defaultAmount=10000
	}
	
	Rule{	
		// Deposit payment
		depositPay:
		Con: !isTime(signTime+period),
		Buyer transfer(Seller,Proportion*o Service.price);
		
		// Late deposit payment
		depositPay_Late:
		Con: isTime(signTime+period) and !isDone(depositPay),
		Seller change(ContractState,terminate);
		
		// Balance payment
		balancePay:
		Con: isDone(depositPay) and logic(now,startTime,==),
		Buyer transfer(Seller,o Service.price-Proportion*o Service.price);
		
		// Delivery
		delivery:
		Con:isDone(balancePay),
		Seller;
		
		// Confirm receipt
		confirm:
		Con:isDone(delivery),
		Buyer;
		
		// Delivery failure due to force majeure
		fail_delivery:
		// Late days for delivery
		setMessage(new uint lateDay)
		Con: !isDone(confirm) and isTime(startTime),
		Seller transfer(Buyer,lateDay*default);
		
		// Extended performance
		extended:
		setMessage(uint dayNum)
		Con:isDone(confirm),
		Buyer
		sub{
			Con:logic(dayNum,1,==),
			transfer(Seller,extend1);
		}
		sub{
			Con:logic(dayNum,1,!=),
			transfer(Seller,extend1+extend2*dayNum-extend3);
		};
		
		// Check device status
		checkDevice:
		setMessage(bool checkStatus,ufixed deviceAmount)
		Con: isTime(finishTime+dayNum),
		Seller;
		
		// Device is good, contract ends
		status_good:
		Con: isDone(checkDevice) and isTrue(checkStatus),
		Buyer change(ContractState,finish);
		
		// Device has issues, pay damages, contract ends
		status_bad:
		Con:isDone(checkDevice) and !isTrue(checkStatus),
		Buyer transfer(Seller,deviceAmount) and change(ContractState,finish);
		
		// Seller terminates early
		terminate_Seller:
		Con: !isTime(finishTime+dayNum),
		Seller transfer(Buyer,defaultAmount) and change(ContractState,terminate);
		
		// Buyer terminates early
		terminate_Buyer:
		Con: !isTime(finishTime+dayNum),
		Buyer transfer(Seller,defaultAmount) and change(ContractState,terminate);	

	}
	
	require:isDone,isTime,isTrue,transfer;
}
