Contract HouseRent{
	PA Person
	Token House{
	  string location,
	  string usage
	}
	init {
		s Person Landlord = ('Fengshan Town Vegetable Village Joint Stock Economic Cooperative',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		s Person Tenant = ('Luotian County Statistics Bureau',
			a'0x5B38Da6a701c568545dCfcB03FcB875f56beddC4')
		o House house = ('Lijiawan','office')
	}
	ContractMessage{ 
	  uint penalty=3000,
	  uint management_fee=4000,
	  duration payduration=365 days,
	  date startTime=2020/10/16,00:00,
	  date finishTime=2023/10/15,00:00,
	  date payTime=2020/10/16,00:00,
	  uint time1=90 days,
	  uint time2=15 days,
	  bool continue=false,
	  uint amount=0
	}
	
	Rule{
		// Payment of the first year's management fee
		rule1:
		Con: !isTime(startTime),	
		Tenant transfer(Landlord,management_fee);
		
		// Payment of the following years' management fees
		repeat
		rule2:
		Con: isDone(rule1) and !isTime(payTime+payduration),
		Tenant transfer(Landlord,management_fee) and change(payTime,now);
		
		// Determine whether to renew the contract at the end of the term for one more year
		rule3:
		setMessage(bool continue)
		Con: !isTime(finishTime),
		Tenant
		sub{
			Con: isTrue(continue),
			transfer(Landlord,management_fee) and change(finishTime,finishTime+payduration);
		};
		
		// Within 15 days after the contract ends, the landlord checks the house and determines any damage amount
		rule4:
		setMessage(uint amount)
		Con: isTime(finishTime) and !isTime(finishTime+time2),
		Landlord;
		
		// Tenant returns the house, and the contract ends
		rule5:
		Con: isDone(rule4),
		Tenant transfer(Landlord,amount) and change(ContractState,finish);
		
		// Tenant delays payment of the management fee for more than three months, Landlord terminates the contract
		rule6:
		Con: isDone(rule1) and isTime(payTime+payduration+time1),
		Landlord change(ContractState,terminate);
		
		// Early termination of the contract by either party with a compensation of 3000
		rule7:
		Con: !isTime(finishTime),
		Landlord transfer(Tenant,penalty) and change(ContractState,terminate);
		
		rule8:
		Con: !isTime(finishTime),
		Tenant transfer(Landlord,penalty) and change(ContractState,terminate);
		
	}
	require:isDone,isTime,isTrue,transfer;
}
