contract regularPurchase{
	
 party Seller{
  name:String
  amount:Money
  delivery()
  suspendDelivery1(lateDays:Integer)
  suspendDelivery2()
  endSale1()
 }
 
 party Buyer{
  name:String
  amount:Money
  confirmBuy()
  confirmGet()
  endSale2()
 }
 
 party Platform{
 	amount:Money
 	confirmArrive()
 	endSale31()
 	endSale32()
 	endSale33()
 	endSale34()
 }
 
 thing: Thing
 info1:contractInfo
 lateIR:Float
 finishIR:Float
 
 term no1: Buyer can confirmBuy
 while transfer $info1::Price to Platform::amount.
 
 term no2: Seller must delivery
 when before info1::giveDate 
 and after Buyer did confirmBuy.
 
 term no3: Platform can confirmArrive
 when after (Seller did delivery) or (after Seller did suspendDelivery1).
 
 term no4: Buyer can confirmGet
 when within 15 day after Platform did confirmArrive
 where thing::ownership = Buyer.
 
 term no5: Platform can endSale31
 when after Buyer did confirmGet
 while transfer $info1::Price to Seller::amount.
 
 term no6: Platform can endSale32
 when after 15 day after Platform did confirmArrive
 while transfer $info1::Price to Seller::amount
 where thing::ownership = Buyer.
 
 // Unable to express 
 term no7: Seller can suspendDelivery1
 when within 5 day after info1::giveDate and after Buyer did confirmBuy
 //while transfer $info1::Price*lateIR*lateDays to Buyer::amount.
 while transfer $info1::Price*lateIR to Buyer::amount.
 
 // Unable to express 
 term no8: Seller can suspendDelivery2
 when after 5 day after info1::giveDate and after Buyer did confirmBuy
 while transfer $info1::Price*lateIR to Buyer::amount.
 
 term no9: Platform can endSale33
 when after Seller did suspendDelivery2
 while transfer $info1::Price to Buyer::amount.
 
 term no10: Buyer can endSale2
 while transfer $info1::Price*finishIR to Seller::amount.
 
 term no11: Seller can endSale1
 while transfer $info1::Price*finishIR to Buyer::amount.
 
 term no12: Platform can endSale34
 when after Buyer did endSale2 or after Seller did endSale1
 while transfer $info1::Price to Buyer::amount.
 
  type contractInfo{
  giveDate:Date
  Price:Integer
  }
 
  asset Thing{
  info
  {
   name:String
   unit_price:Integer
   number:Integer
  }
  right{
   ownership:Right
  }
 }

 }
